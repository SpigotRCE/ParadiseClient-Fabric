plugins {
    id 'fabric-loom' version '1.8.9'
    id 'maven-publish'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
}

version = project.mod_version
group = project.maven_group

base {
    archivesName = project.archives_base_name
}

repositories {
    maven {
        url 'https://jitpack.io'

        content {
            includeGroup "com.github.Oryxel"
        }
    }

    maven {
        name = 'ViaVersion'
        url = 'https://repo.viaversion.com/'
    }

    maven {
        name = "modrinth"
        url = "https://api.modrinth.com/maven"
    }

    maven {
        name = "Lenni0451"
        url = "https://maven.lenni0451.net/everything"
    }

    maven {
        name = "OpenCollab Releases"
        url = "https://repo.opencollab.dev/maven-releases"
    }

    maven {
        name = "OpenCollab Snapshots"
        url = "https://repo.opencollab.dev/maven-snapshots"
    }

    flatDir {
        dirs 'libs'
    }
}

dependencies {
    // To change the versions see the gradle.properties file
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
    // Fabric API. This is technically optional, but you probably want it anyway.
    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"
    implementation 'org.apiguardian:apiguardian-api:1.1.2'
//    implementation 'com.github.JnCrMx:discord-game-sdk4j:v0.5.5'
    shadow(implementation 'io.github.spigotrce.eventbus:EventBus:1.0-SNAPSHOT')
    shadow(implementation 'net.md_5.bungee:BungeeCord')

//    include("org.cloudburstmc.netty:netty-transport-raknet:1.0.0.CR3-SNAPSHOT") {
//        exclude group: "io.netty"
//    }
//
//    modImplementation "de.florianmichael:ViaFabricPlus:${project.viafabricplus_version}"
}

processResources {
    inputs.property "version", project.version
    inputs.property "minecraft_version", project.minecraft_version
    inputs.property "loader_version", project.loader_version
    filteringCharset "UTF-8"

    filesMatching("fabric.mod.json") {
        expand "version": project.version,
                "minecraft_version": project.minecraft_version,
                "loader_version": project.loader_version
    }
}

def targetJavaVersion = 21
tasks.withType(JavaCompile).configureEach {
    it.options.encoding = "UTF-8"
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        it.options.release.set(targetJavaVersion)
    }
}

java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
    withSourcesJar()
}

jar {
    from("LICENSE") {
        rename { "${it}_${project.archivesBaseName}" }
    }
    archiveClassifier.set("dev")
}
shadowJar {
    configurations = [project.configurations.shadow]
    archiveClassifier.set("dev")
}

remapJar {
    dependsOn(shadowJar)
    inputFile = tasks.shadowJar.archiveFile
}

// configure the maven publication
publishing {
    publications {
        create("mavenJava", MavenPublication) {
            artifactId = project.archives_base_name
            from components.java
        }
    }
}
